#Include "MathLib" as MathLib
#Include "TextLib" as TextLib

#Include "Libs/BigBang1112/Layers.Script.txt" as Layers
#Include "Libs/BigBang1112/Manialink.Script.txt" as Manialink
#Include "Libs/BigBang1112/Settings.Script.txt" as Settings

Void Start() {
	declare Integer LargestCampaignHeight;
	foreach(Campaign,DataFileMgr.Campaigns) {
		declare Height = 0;
		foreach(MapGroup,Campaign.MapGroups) {
			Height += 15;
			Height += MathLib::FloorInteger(MapGroup.MapInfos.count/5.)*45;
		}
		
		if(Height > LargestCampaignHeight) {
			LargestCampaignHeight = Height;
		}
	}
	
	declare CampaignListBuild = """<frame id="FRAME_CAMPAIGN_MENU">""";
	CampaignListBuild ^= """<quad id="QUAD_CAMPAIGN_MENU" pos="-112.5 7.5" size="{{{DataFileMgr.Campaigns.count*45*5}}} {{{LargestCampaignHeight}}}" z-index="-1" scriptevents="1"/>""";
	declare CampaignPadBuild = """<frame id="FRAME_CAMPAIGNS">""";

	for(I, 0, DataFileMgr.Campaigns.count-1) {
		declare Campaign <=> DataFileMgr.Campaigns[I];
		CampaignListBuild ^= "\n"^"""	<frame pos="{{{I*45*5}}} 0">""";
		CampaignPadBuild ^= """<quad size="45 20" halign="center" valign="center" image="file://Media/Images/ScreenShot02.jpg" scriptevents="1" id="QUAD_CAMPAIGN" pos="{{{I*45}}}"/>""";
		declare TotalMaps = 0;
		for(J,0,Campaign.MapGroups.count-1) {
			declare MapGroup <=> Campaign.MapGroups[J];
			declare GroupPositionY = MathLib::FloorInteger(TotalMaps/5.)*(-45)-J*15;
			TotalMaps += MapGroup.MapInfos.count;
			
			CampaignListBuild ^= "\n"^"""	<frame pos="0 {{{GroupPositionY}}}" z-index="2">
			<quad z-index="0" size="225 15" style="Bgs1" substyle="BgMetalBar" halign="center" valign="center" scriptevents="1"/>
			<label z-index="1" size="215 10" text="$o$iMAPGROUP{{{J}}}" halign="center" valign="center2" textsize="6" textfont="RajdhaniMono" pos="0 -0.4"/>
		</frame>""";
		
			CampaignListBuild ^= "\n"^"""		<frame pos="0 {{{GroupPositionY-30}}}">""";
			
			for(K,0,MapGroup.MapInfos.count-1) {
				declare MapInfo <=> MapGroup.MapInfos[K];
				declare ThumbnailPositionX = -90+(K%5)*45;
				
				declare ThumbnailPositionY = MathLib::FloorInteger(K/5.)*-45;
				CampaignListBuild ^= "\n"^"""			<frame pos="{{{ThumbnailPositionX}}} {{{ThumbnailPositionY}}}">
				<quad z-index="0" size="45 45" style="Bgs1" substyle="BgMetalBar" halign="center" valign="center" image="file://Thumbnails/MapUid/{{{MapInfo.MapUid}}}" scriptevents="1"/>
			</frame>""";
			}
			
			CampaignListBuild ^= "\n"^"""		</frame>""";
		}
		CampaignListBuild ^= "\n"^"""	</frame>""";
	}

	CampaignListBuild ^= "\n"^"""</frame>""";
	CampaignPadBuild ^= "\n"^"""</frame>""";
	
	declare CampaignUi = Manialink::FromFile("Media/Manialinks/Campaign", True, [], Settings::Get().Theme);
	CampaignUi = TextLib::Replace(CampaignUi, """<frame id="FRAME_CAMPAIGN_MENU"/>""", CampaignListBuild);
	CampaignUi = TextLib::Replace(CampaignUi, """<frame id="FRAME_CAMPAIGNS"/>""", CampaignPadBuild);
	Layers::Create("Campaign", CampaignUi, True);
}

Void Event(CManiaAppEvent _Event) {
	declare Event <=> _Event;

	if(Event.Type == CManiaAppEvent::EType::LayerCustomEvent) {
		switch(Event.CustomEventType) {
			case "Button_Exit": {
				Menu_Quit();
			}
		}
	}
}

Void Async() {
	
}

Void Loop() {

}