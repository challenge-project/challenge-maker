#RequireContext CManiaAppTitle

#Include "TextLib" as TextLib
#Include "ColorLib" as ColorLib
#Include "TimeLib" as TimeLib

#Include "Libs/BigBang1112/Http.Script.txt" as Http
#Include "Libs/BigBang1112/Layers.Script.txt" as Layers
#Include "Libs/BigBang1112/Manialink.Script.txt" as Manialink
#Include "Libs/BigBang1112/Auth.Script.txt" as Auth
#Include "Libs/BigBang1112/Auth2.Script.txt" as Auth2
#Include "Libs/BigBang1112/Dictionary.Script.txt" as Dictionary
#Include "Libs/BigBang1112/Settings.Script.txt" as Settings
#Include "Libs/BigBang1112/File.Script.txt" as File
#Include "Libs/BigBang1112/Time.Script.txt" as Time

#Include "Libs/Campaign/Menu/Campaign.Script.txt" as Menu_Campaign
#Include "Libs/Campaign/Menu/PreCampaign.Script.txt" as Menu_PreCampaign
#Include "Libs/Campaign/Menu/Leaderboards.Script.txt" as Menu_Leaderboards

#Include "Libs/Challenge/Menu/AuthBeta.Script.txt" as Menu_AuthBeta
#Include "Libs/Challenge/Menu/Replays.Script.txt" as Menu_Replays
#Include "Libs/Challenge/Menu/Release.Script.txt" as Menu_Release
#Include "Libs/Challenge/Notification.Script.txt" as Notification
#Include "Libs/Challenge/DevMode.Script.txt" as DevMode

#Include "Libs/Maker/MakerBase.Script.txt" as MakerBase

#Include "Libs/Challenge/Service/Status.Script.txt" as Service_Status

#Setting S_Dev False
#Setting S_OfflineMode False

Void GetUnauthorizedToken(Service_Status::SStatus _Status) {
	declare Request = Http::SyncGET("""{{{_Status.Global.Service}}}/auth.php?login={{{LocalUser.Login}}}&token={{{Auth::GetToken().Value}}}""");
	if(Http::IsSuccessfulStatusCode(Request.StatusCode)) {
		declare Menu_AuthBeta::SAccess Access;
		declare JsonSuccess = Access.fromjson(Request.Result);
		declare Text CHALLENGE_TOKEN for LocalUser;
		CHALLENGE_TOKEN = Access.UserToken;
	}
	else Dialog_Message("HTTP ERROR "^Request.StatusCode^"\nTry to reload the title pack.");
	Http.Destroy(Request);
}

main() {
	while(True) { // Restart mechanism
		Settings::Restart(False);
		
		Dictionary::SetTranslationFolder("Media/Translations");
		Dictionary::SetTranslationLanguage(Settings::Get().Language);
		
		EnableMenuNavigationInputs = True;
		
		if(S_Dev) DevMode::Start();
		
		declare Authorized = False;
		
		if(!S_OfflineMode) {
			Auth::Start();
			
			Notification::Init();
			yield;
			
			Layers::Create("Loading", Manialink::FromFile("Media/Manialinks/Loading", False, [], Settings::Get().Theme), True);
			
			declare Status = Service_Status::CheckStatus(LocalUser, Auth::GetToken().Value);
			while(!Status.Global.Running) {
				sleep(10000);
				Status = Service_Status::CheckStatus(LocalUser, Auth::GetToken().Value);
			}
			
			if(Status.User.count > 0) {
				if(Status.User[0].Authorized) {
					declare Success = Auth2::AsyncAuthorize("Authorize", "b1a3d10ba7", "basic maps", Status.Global.Service^"/auth.php");
					while(True) {
						if(Auth2::IsCompleted("Authorize")) {
							if(Auth2::IsSuccessful("Authorize")) {
								declare Result = Auth2::GetAsyncAuthorizeResult("Authorize");
								if(Result.AlreadyAccepted) {
									declare Success = Http::AsyncGET("Redirect", Result.RedirectUrl);
								}
							}
							else {
								Dialog_Message("HTTP ERROR "^Auth2::GetStatusCode("Authorize")^"\nAuthorization error.");
							}
							Auth2::Destroy("Authorize");
						}
						
						if(Http::IsCompleted("Redirect")) {
							if(Http::IsSuccessful("Redirect")) {
								declare Menu_AuthBeta::SAccess Access;
								declare JsonSuccess = Access.fromjson(Http::GetResult("Redirect"));
								declare Text CHALLENGE_TOKEN for LocalUser;
								CHALLENGE_TOKEN = Access.UserToken;
								break;
							}
							else {
								Dialog_Message("HTTP ERROR "^Auth2::GetStatusCode("Redirect")^"\nRedirection error.");
							}
							Http::Destroy("Redirect");
						}
						
						Auth::Loop();
						yield;
					}
					Authorized = True;
				}
				else {
					GetUnauthorizedToken(Status);
				}
			}
			else {
				GetUnauthorizedToken(Status);
			}
			
			Layers::Hide("Loading", True);
	
			Menu_Release::Start();
			wait(Menu_Release::Loop());
		}
		
		yield;
		
		Layers::Create("Preload", Manialink::FromFile("Media/Manialinks/Preload", False, [], Settings::Get().Theme), True);
		sleep(500);
		Layers::Destroy("Loading");
		
		declare MakerBase::SChallengeTitleSettings Settings for This;
		declare JsonSuccess2 = Settings.fromjson(File::Read("Media/Settings.json"));
		
		declare MakerBase::SCampaign[] Campaigns for This;
		declare JsonSuccess = Campaigns.fromjson(File::Read(MakerBase::GetCampaignsFile()));
		
		declare MakerBase::SChallenge[][] Challenges for This;
		declare JsonChallenges = Challenges.fromjson(File::Read("Media/Challenges.json"));
		Layers::SendEvent("Preload", "Progress", 1/8.);
		yield;
		Menu_PreCampaign::Init();
		Layers::SendEvent("Preload", "Progress", 2/8.);
		yield;
		Menu_Campaign::Init();
		Layers::SendEvent("Preload", "Progress", 3/8.);
		yield;
		Menu_Campaign::InitBuilder();
		Layers::SendEvent("Preload", "Progress", 4/8.);
		yield;
		Menu_AuthBeta::Init();
		Layers::SendEvent("Preload", "Progress", 5/8.);
		yield;
		Menu_Replays::Init();
		Layers::SendEvent("Preload", "Progress", 6/8.);
		yield;
		Menu_Leaderboards::Init();
		Layers::SendEvent("Preload", "Progress", 7/8.);
		yield;
		Layers::Create("Intro", Manialink::FromFile("Media/Manialinks/Campaign/Intro", True, [], Settings::Get().Theme));
		Layers::SendEvent("Preload", "Progress", 8/8.);
		sleep(300);
		Layers::Hide("Preload", True);
		sleep(200);
		Layers::Show("Intro", True);
		declare IntroStart = Now;
		while(Now - IntroStart  < 5000) {
			foreach(Event in PendingEvents) {
				if(Event.Type == CManiaAppEvent::EType::MenuNavigation) {
					if(Event.MenuNavAction == CManiaAppEvent::EMenuNavAction::Cancel) {
						Layers::Hide("Intro");
						IntroStart -= 5000;
					}
				}
			}
			yield;
		}
					
		Layers::Hide("Intro", True);
		EnableMenuNavigationInputs = False;
		
		Menu_PreCampaign::Start();
		Menu_Campaign::Start();
		Notification::Start(<152.5,72.5>);
		if(Authorized) Layers::SendEvent("Top", "HideJoin");
	
		while(!Settings::IsRestarting()) {
			foreach(Event,PendingEvents) {
				Layers::Event(Event);
				Menu_PreCampaign::Event(Event);
				Menu_Campaign::Event(Event);
				Menu_AuthBeta::Event(Event);
				Menu_Replays::Event(Event);
				Menu_Leaderboards::Event(Event);
			}
			
			Menu_Campaign::Async();
			Menu_PreCampaign::Async();
			Menu_Replays::Async();
			Menu_Leaderboards::Async();
			Menu_AuthBeta::Async();
	
			Auth::Loop();
			Menu_PreCampaign::Loop();
			Menu_Campaign::Loop();
			Menu_AuthBeta::Loop();
			Menu_Replays::Loop();
			Menu_Leaderboards::Loop();

			if(!Time::Stamp_CreateOnce("STATUS", 10*1000)) {
				if(Time::Stamp_Reached("STATUS")) {
					declare Success = Service_Status::AsyncCheckStatus("STATUS", LocalUser);
				}
			}
			
			if(Service_Status::AsyncIsCompleted("STATUS")) {
				if(Service_Status::AsyncIsSuccessful("STATUS")) {
					declare Status = Service_Status::GetAsyncCheckStatusResult("STATUS", LocalUser);
				}
			}
		
			yield;
		}
		
		Layers::DestroyAll();
		UILayerDestroyAll();
	}
}